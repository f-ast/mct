include "Java/java.grm"
#pragma -comment
#ifdef COMMENTS
include "Java/javaCommentOverrides.grm"
#endif

% Yijun Yu: May 2011
% redefine method_body
%         [repeat block preferred with '; ]
%     |  [opt annotation_default] ';             % T.D. Oct 2007
% end define

redefine field_declaration
   [comment_nl] ... 
end define

redefine modifier
        'abstract
    |   'final
    |   'public
    |   'protected
    |   'private
    |   'static
    |   'transient
    |   'volatile
    |   'native
    |   'synchronized
    |   'strictfp		% Added strictfp XG July 23, 2002
    |   [repeat annotation ignored]		% T.D. Oct 2007
end define

redefine program
    [package_declaration*]
end define

redefine type_declaration
        [class_declaration]      [NL][NL]
#ifdef COMMENTS
    [comment_nl]
#endif
    |   [interface_declaration]  [NL][NL]
#ifdef COMMENTS
    [comment_nl]
#endif
    |   [enum_declaration]	    [NL] % T.D. Oct 2007
#ifdef COMMENTS
    [comment_nl]
#endif
end define

%redefine enum_body
%    '{                                    [IN]
%       [list enum_element] [opt ',]
%       [repeat class_body_declaration ordered ]    [EX]
%    '} [opt ';]                           [NL][NL]
%end define

% remove some extra spaces
redefine import_declaration
#ifdef COMMENTS
    [comment_nl]
#endif
	'import [opt 'static] [imported_name] '; [NL]
#ifdef COMMENTS
    [comment_nl]
#endif
    | 	
#ifdef COMMENTS
    [comment_nl]
#endif
'; [NL] 	% JRC Apr 08
#ifdef COMMENTS
    [comment_nl]
#endif
end define

redefine class_header
    [repeat modifier ordered] 'class [class_name] [opt extends_clause] [opt implements_clause]
end define

% T.D. Oct 2007 -- added annotation marker 
redefine interface_header
    [repeat modifier ordered] [opt annot_marker] 'interface [interface_name] [opt extends_clause] [opt implements_clause]
end define

redefine enum_header
    [repeat modifier ordered] [opt id] 'enum [opt declared_name] [opt extends_clause] [opt implements_clause]
end define

redefine extends_clause
    'extends [list type_name+ ordered]
end define

redefine implements_clause
    'implements [list qualified_name+ ordered]
end define

redefine package_declaration
#ifdef COMMENTS
    [comment_nl] [NL]
#endif
    [opt package_header scoped]
    [repeat import_declaration] 
    [repeat type_declaration scoped]
end define

redefine class_or_interface_body
    '{                                    [NL][IN]
       [repeat class_body_declaration scoped ordered preferred with [UserModifiedComment] [UserModifiedCommentNot] ]    [EX] [NL]
#ifdef COMMENTS
    [comment_nl]
#endif
    '} [opt ';]
#ifdef COMMENTS
    [comment_nl]
#endif
[NL][NL]
end define

% to match Java Code like:
% public <T> void someMethod(T t) {
%	// ...
% }
redefine constructor_declaration
    [NL] [repeat modifier ordered] [opt generic_parameter] [constructor_declarator] [opt throws] [constructor_body] [NL][NL]
end define

redefine variable_declaration
    [repeat modifier ordered] [type_specifier] [variable_declarators] '; [NL]
end define

redefine variable_declarators
    [list variable_declarator+ ordered]
end define

% Method declarations 

% generic method declaration (to match generic types used only in a method's declaration and body)
redefine method_declaration
    [NL] [repeat modifier ordered] [opt generic_parameter] [type_specifier] [method_declarator] [opt throws] [method_body]
end define

redefine throws
    'throws [list qualified_name+ ordered]
end define

redefine annotation_value_list
      [single_annotation_value]	% for single value annotation
    | [list keyed_annotation_value ordered]
end define

redefine single_annotation_value
      [expression]					% must be constant
    | [nested_annotation]
    | { [list expression_or_nested_annotation ordered] }	% array of annotation values
end define

redefine comment_nl
    [empty] | [repeat mycomment scoped]
end define

function contains Object [any] 
    match * [any] Object
end function

function Private A  [class_body_declaration]
    match  [class_or_interface_body]
        B  [class_or_interface_body] 
    construct M  [modifier *] _  [^ A] 
    construct PublicModifiers [modifier*] 'public 'protected 
    where not M [contains each PublicModifiers]
end function

function NotJavaDoc A  [mycomment]
    match  [comment_nl]
        B  [comment_nl] 
    construct S_Comment [stringlit] _ [quote A] 
    where not S_Comment [grep "\/\*\*"]
end function

function NotGeneratedNot A  [class_body_declaration]
    match  [class_or_interface_body]
        B  [class_or_interface_body] 
    construct S_Cs [stringlit] _ [quote A]
    where not S_Cs [grep "@generated INV"]
    where not S_Cs [grep "@generated NOT"]
end function

function remove_comment
    replace * [comment_nl] X [comment_nl]
    by _
end function

function put_string F [stringlit] S [stringlit]
   replace [stringlit] Str [stringlit]
   construct d_S [stringlit] S [fput F]
   by Str
end function

function diff S1 [stringlit] S2 [stringlit]
   replace [any] S [any]
   where not S1 [= ""]
   construct file1 [stringlit] "t.1"
   construct file2 [stringlit] "t.2"
   construct diffCmd [stringlit] _ [+ "diff "] [+ file1] [+ " "] [+ file2]
   construct d_S1 [class_body_declaration] _ [parse S1] [write file1]
   construct d_S2 [class_body_declaration] _ [parse S2] [write file2] [system diffCmd]
   by S
end function

function UserModifiedComment 
    replace [class_body_declaration*] A1 [class_body_declaration] A2 [class_body_declaration*]
    construct nc_X [class_body_declaration] A1 [remove_comment]
    construct s_X [stringlit] _ [unparse nc_X] % [print]
    construct S_Cs [stringlit] _ [quote A1] 
    where S_Cs [grep "@generated"]
    where S_Cs [grep "<!-- begin-user-doc -->"]
    where S_Cs [grep "<!-- end-user-doc -->"]
    construct L_begin [number] _ [# "<!-- begin-user-doc -->" ]
    construct I_begin [number] _ [index S_Cs "<!-- begin-user-doc -->" ] [+ L_begin] [+ 1]
    construct I_end [number] _ [index S_Cs "* <!-- end-user-doc -->" ] [- 5]
    construct S_code [stringlit] S_Cs [: I_begin I_end] 
    construct P [any] _ [diff S_code s_X]
    construct B1 [class_body_declaration] A1 % [print]
    construct B2 [class_body_declaration*] A2 [UserModifiedComment]
    by _ [. B1] [. B2]
end function

function UserModifiedCommentNot
    replace [class_body_declaration*] A1 [class_body_declaration] A2 [class_body_declaration*]
    construct S_Cs [stringlit] _ [quote A1] 
    where not S_Cs [grep "@generated"]
    construct B2 [class_body_declaration*] A2 [UserModifiedComment] [UserModifiedCommentNot]
    by _ [. A1] [. B2]
end function

