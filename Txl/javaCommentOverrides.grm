% Overrides to preserve comments in Java
% Jim Cordy, March 2001 (Rev Feb 2003)

#pragma -comment

% Can have comments as statements 
redefine statement
	... [comment_NL]
end redefine

redefine package_declaration
	[opt comment_NL] ...
end redefine

% Can have comments as declarations
redefine class_body_declaration
        [comment_NL] ... 
end redefine

%redefine class_body_declaration
%	... [comment_NL]
%end redefine

redefine import_declaration
	... [comment_NL] 
end redefine

redefine type_declaration
	... [comment_NL] 
end redefine

% Can have comments before initializers ...
redefine variable_initializer
	[opt comment_NL] ...
end redefine

% ... or after initializers ...
% redefine variable_initializer
% 	... [opt comment_NL]
% end redefine

% ... or before =initializers 
redefine equals_variable_initializer
	[opt comment_NL] ...
end redefine

% Can have comments before formal parameters ...
redefine formal_parameter
	[opt comment_NL] ...
end redefine

% ... or after formal parameters ...
% redefine formal_parameter
% 	...  [opt comment_NL]
% end redefine

% ... or after a parameter list 
redefine constructor_declarator
	... [opt comment_NL]
end redefine

redefine method_declarator
	... [opt comment_NL]
end redefine

% Can have comments before arguments ...
redefine argument
	[opt comment_NL] ...
end redefine

% ... or after arguments ...
% redefine argument
% 	...  [opt comment_NL]
% end redefine

% ... or after an argument list 
redefine method_argument
	... [opt comment_NL]
end redefine

% Can have comments after an if condition
redefine if_statement
    'if '( [expression] ')	[opt comment_NL]
		[statement]
	[opt else_clause]   [NL]
end redefine

% Can have comments on a block
redefine block
	[opt comment_NL] ...
end redefine

% Can have comments before an expression
redefine expression
	[opt comment_NL] ...
end redefine

define my_comment
    [comment]
end define
% Need newlines after comments
define comment_NL
#ifdef DEFINE
	[repeat my_comment kept] [NL]
#else
	[repeat my_comment] [NL]
#endif
end define
