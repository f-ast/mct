% Overrides to preserve comments in Java
% Jim Cordy, March 2001 (Rev Feb 2003)

#pragma -comment

% Can have comments as statements 
redefine statement
	... [comment_NL]
end define

redefine statement
	[comment_NL] ... 
end define

redefine package_declaration
	[comment_NL] ...
end define

% Can have comments as declarations
redefine class_body_declaration
        [comment_NL] ... 
end define

redefine class_body_declaration
 	... [comment_NL]
end define

redefine import_declaration
	[comment_NL] ... 
end define

redefine import_declaration
	... [comment_NL] 
end define

redefine type_declaration
	... [comment_NL] 
end define

redefine class_or_interface_body
    '{                                    [NL][IN]
       [repeat class_body_declaration]    [EX]
       [comment_NL]
    '} [opt ';] [comment_NL]                            [NL][NL]
end define

% ... or after initializers ...
redefine variable_initializer
 	... [comment_NL]
end define

% Can have comments before initializers ...
redefine variable_initializer
	[comment_NL] ...
end define

% ... or before =initializers 
redefine equals_variable_initializer
	[comment_NL] ...
end define

% ... or after formal parameters ...
redefine formal_parameter
 	...  [comment_NL]
end define

% Can have comments before formal parameters ...
redefine formal_parameter
	[comment_NL] ...
end define

% ... or after a parameter list 
redefine constructor_declarator
	... [comment_NL]
end define

redefine method_declarator
	... [comment_NL]
end define

% ... or after arguments ...
redefine argument
 	...  [comment_NL]
end define

% Can have comments before arguments ...
redefine argument
	[comment_NL] ...
end define

% ... or after an argument list 
redefine method_argument
	... [comment_NL]
end define

% Can have comments after an if condition
redefine if_statement
    'if '( [expression] ')	[comment_NL]
		[statement]
	[opt else_clause]   [NL]
end define

% Can have comments on a block
redefine block
	[comment_NL] ...
end define

% Can have comments before an expression
redefine expression
	[comment_NL] ...
end define

% Need newlines after comments
define comment_NL
	[repeat comment] [NL]
end define
