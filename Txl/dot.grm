#pragma -width 1024
keys 
  'digraph 'node 'edge 'graph 'subgraph '-> ': '--
end keys
tokens
    id  "[$\u][$\i]*"
end tokens

define graph_type
	'digraph | 'graph
end define

define graph
   [NL] [graph_type] [id] '{ [IN] 
    [opt graph_attributes]
    [opt node_description]
    [opt edge_description]
    [opt graph_description]
    [node_or_edge*]
   [NL] [EX] '}
end define

define node_description
   [NL] 'node [attributes] [opt ';]
end define

define edge_description
   [NL] 'edge [attributes] [opt ';]
end define

define graph_description
   [NL] 'graph [attributes] [opt ';]
end define

define subgraph
   [NL] 'subgraph [id] '{ [IN]
    [opt attribute]
    [opt node_description]
    [opt edge_description]
    [node_or_edge*]
   [NL] [EX] '} 
end define

define attributes
 '[ [list attribute] ']
end define

define graph_attributes
 [attribute*] 
end define

define attribute
   [id] [SPOFF] '= [stringlit] [SPON] 
 | [id] [SPOFF] '= [id*] [SPON] 
end define

define node_or_edge
  [node] | [edge] | [subgraph]
end define

define node
   [NL]  [id] [opt attributes] [opt ';]
 | [NL] [stringlit] [opt attributes] [opt ';]
end define

define edge_target
   [SP] [SPOFF] '-> [SPON] [node_id] [SPON] 
 | [SP] [SPOFF] '-- [SPON] [node_id] [SPON] 
end define

define edge
     [NL]  [node_id] [edge_target+] [opt attributes] [opt ';]
end define

define node_id
      [id]
    | [id] [SPOFF] ': [id] [SPON] 
    | [stringlit] 
end define
