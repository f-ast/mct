% #pragma -w 32000 
% Assumes that the input program doesn't have identifiers named INDENT or DEDENT
keys
    INDENT DEDENT
end keys

tokens
    docend "..."
    docsep "---"
    id  "[$\u'][$\u0123456789'-.]*"
end tokens

comments
    '#
    /*  */
    //
end comments

redefine indent
    % By using [attr], the INDENT does not appear in output
    [attr 'INDENT] [IN]
end define

redefine dedent
    % By using [attr], the DEDENT does not appear in output
    [attr 'DEDENT] [EX]
end define

define yaml
  [document*]
end define

define docstart
[NL] [docsep] [indent] [EX]
end define

define docfin
[NL] [IN] [dedent][docend] 
end define

define document
  [opt comment] [opt docstart] [node] [opt docfin]
end define

define node
%  [empty]
% |
  [Scalar]
 |[List] 
 |[Dict]
end define

define Scalar
[stringlit]
| [number]
| [id+]
end define

define List
      [ListNode*] 
   |  [indent] [ListNode+] [dedent]
   | '[ [node] [comma_node*] ']
end define

define ListNode
 [NL] '- [node] 
end define 

define comma_node
    ', [SP] [node]
end define

define Dict
  '{ [opt pair] [comma_pair*] '} 
 | [pair] [indent] [indent_pairs] [dedent]
 | [indent] [indent_pairs] [dedent]
end define

define pair
  [Scalar] [SPOFF] ': [SPON] [SP] [node]
end define

define indent_pairs
  [NL_pair+] 
end define

define comma_pair
	', [SP] [pair]
end define

define NL_pair
	[NL] [pair] 
end define

